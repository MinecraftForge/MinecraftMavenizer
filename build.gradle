plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.licenser' version '1.1.1'
    id 'net.minecraftforge.gradleutils' version '2.4.9'
    id 'com.gradleup.shadow' version '8.3.6'

    id 'org.gradlex.extra-java-module-info' version '1.8'
}

final displayName = 'Minecraft Mavenizer'
description = 'A pure-blooded Java tool to generate a maven repository for Minecraft artifacts.'
group = 'net.minecraftforge'
version = gitversion.version.tagOffset
println('Version: ' + version)

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    withSourcesJar()
}

extraJavaModuleInfo {
    failOnMissingModuleInfo = false

    automaticModule(libs.diff, 'io.codechicken.diffpatch')
    automaticModule(libs.srgutils, 'net.minecraftforge.srgutils')
}

repositories {
    maven gradleutils.forgeMaven
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation libs.jopt
    implementation libs.gson
    implementation libs.jver
    implementation libs.srgutils
    implementation libs.diff

    implementation libs.fastcsv
    implementation libs.commonsio

    // Static Analysis
    compileOnly libs.nulls
}

license {
    header project.file('LICENSE-header.txt')
    newLine false
    exclude '**/ComparableVersion.java' // Apache class used as source-level dep as to not need the hundred+ other classes the library pulls in
}

jar {
    manifest {
        attributes([
            'Specification-Title':   displayName,
            'Specification-Vendor':  'Forge Development LLC',
            'Specification-Version':  gitversion.version.info.tag,
            'Implementation-Title':  displayName,
            'Implementation-Vendor': 'Forge Development LLC',
            'Implementation-Version': project.version
        ] as LinkedHashMap, 'net/minecraftforge/mcmaven/cli')
    }

    archiveClassifier = 'slim'
    exclude 'net/minecraftforge/mcmaven/cli/**'
}

tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar).configure {
    manifest {
        attributes([
            'Main-Class': 'net.minecraftforge.mcmaven.cli.Main'
        ] as LinkedHashMap)
    }

    enableRelocation = true
    relocationPrefix = 'net.minecraftforge.mcmaven.shadow'
    archiveClassifier = ''
    minimize()
}

artifacts {
    archives shadowJar
}

compileJava {
    options.encoding = 'UTF-8'
}

changelog {
    fromBase()
    publishAll = false
}

publishing {
    publications.register('mavenJava', MavenPublication).configure {
        from components.shadow
        artifact jar

        artifactId = project.name
        changelog.publish it

        pom { pom ->
            name = displayName
            description = project.description

            gradleutils.pom.setGitHubDetails pom

            license gradleutils.pom.licenses.LGPLv2_1

            developers {
                developer gradleutils.pom.Developers.LexManos
                developer gradleutils.pom.Developers.Jonathing
            }
        }
    }
    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

idea.module { downloadSources = downloadJavadoc = true }
