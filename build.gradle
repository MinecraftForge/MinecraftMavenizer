plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.licenser' version '1.1.1'
    id 'net.minecraftforge.gradleutils' version '2.4.9'
    id 'com.gradleup.shadow' version '8.3.6'

    id 'org.gradlex.extra-java-module-info' version '1.8'
}

repositories {
    maven gradleutils.forgeMaven
    mavenCentral()
    mavenLocal()
}

group = 'net.minecraftforge'
version = gitversion.version.tagOffset
println('Version: ' + version)

dependencies {
    implementation libs.jopt
    implementation libs.gson
    implementation libs.jver
    implementation libs.srgutils
    implementation libs.diff

    // FOR MCPNames -- TODO CLEAN UP LATER
    implementation libs.fastcsv
    implementation libs.commonsio

    // Static Analysis
    compileOnly libs.nulls
}

extraJavaModuleInfo {
    failOnMissingModuleInfo = false

    automaticModule(libs.diff, 'io.codechicken.diffpatch')
    automaticModule(libs.srgutils, 'net.minecraftforge.srgutils')
}

license {
    header project.file('LICENSE-header.txt')
    newLine false
    exclude '**/ComparableVersion.java' // Apache class used as source-level dep as to not need the hundred+ other classes the library pulls in
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
    withSourcesJar()
}

jar {
    manifest {
        attributes([
            'Main-Class': 'net.minecraftforge.mcmaven.Main'
        ] as LinkedHashMap)
        attributes([
            'Specification-Title':   'Minecraft Maven',
            'Specification-Vendor':  'Forge Development LLC',
            'Specification-Version':  gitversion.version.info.tag,
            'Implementation-Title':  'Minecraft Maven',
            'Implementation-Vendor': 'Forge Development LLC',
            'Implementation-Version': project.version
        ] as LinkedHashMap, 'net/minecraftforge/mcmaven/')
    }
}

compileJava {
    options.encoding = 'UTF-8'
}

changelog {
    fromBase()
}

publishing {
    publications.register('mavenJava', MavenPublication).configure {
        artifactId = project.name
        from components.java

        pom {
            name = project.name
            description = 'Minecraft Maven Repo Generator'
            url = 'https://github.com/MinecraftForge/minecraft-maven'

            gradleutils.pom.setGitHubDetails(pom, 'minecraft-maven')

            license gradleutils.pom.licenses.LGPLv2_1

            developers {
                developer gradleutils.pom.Developers.LexManos
            }
        }
    }
    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

idea {
    module {
        // IntelliJ IDEA does not do this by itself anymore...
        downloadJavadoc = true
        downloadSources = true
    }
}
