//import net.ltgt.gradle.errorprone.ErrorProneOptions
//import net.ltgt.gradle.nullaway.NullAwayOptions

plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.licenser' version '1.0.1'
    id 'net.minecraftforge.gradleutils' version '2.3.6'
    id 'com.github.johnrengelman.shadow' version '8.1.1'

    // Enforce jSpecify annotations at compile-time
//    id 'net.ltgt.errorprone' version '4.1.0'
//    id 'net.ltgt.nullaway' version '2.1.0'

    // modules
    id 'org.gradlex.extra-java-module-info' version '1.8'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://maven.minecraftforge.net/' }
}

group = 'net.minecraftforge'

version = gradleutils.tagOffsetVersion
println('Version: ' + version)

dependencies {
    implementation libs.jopt
    implementation libs.gson
    implementation libs.jver
    implementation libs.srgutils
    implementation libs.diff

    // FOR jSpecify
    api libs.jspecify.annotations
//    errorprone libs.errorprone.core
//    errorprone libs.nullaway

    // FOR MCPNames -- TODO CLEAN UP LATER
    implementation libs.fastcsv
    implementation libs.commonsio
}

// TODO [MCMaven][Nullability] Add this back in sometime in the future, but only after we add actual API
// and when we do add it back, make it apply ONLY to the API
/*
// Nullability garbage!
tasks.withType(JavaCompile).configureEach {
    options.errorprone { ErrorProneOptions errorProne ->
        errorProne.disableAllChecks = true

        errorProne.nullaway { NullAwayOptions nullAway ->
            nullAway.jspecifyMode = true
            nullAway.warn()
            nullaway.excludedClasses.add('net.minecraftforge.mcmaven.util.ComparableVersion')
        }

//        errorProne.error("FieldCanBeFinal", "MethodCanBeStatic")
    }
}

nullaway.annotatedPackages.add("net.minecraftforge.mcmaven")
 */

extraJavaModuleInfo {
    failOnMissingModuleInfo = false

    automaticModule(libs.diff, 'io.codechicken.diffpatch')
    automaticModule(libs.srgutils, 'net.minecraftforge.srgutils')
}

license {
    header project.file('LICENSE-header.txt')
    newLine false
    exclude '**/ComparableVersion.java' // Apache class used as source-level dep as to not need the hundred+ other classes the library pulls in
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
    withSourcesJar()
}

jar {
    manifest {
        attributes([
            'Main-Class': 'net.minecraftforge.mcmaven.Main'
        ] as LinkedHashMap)
        attributes([
            'Specification-Title':   'Minecraft Maven',
            'Specification-Vendor':  'Forge Development LLC',
            'Specification-Version':  gradleutils.gitInfo.tag,
            'Implementation-Title':  'Minecraft Maven',
            'Implementation-Vendor': 'Forge Development LLC',
            'Implementation-Version': project.version
        ] as LinkedHashMap, 'net/minecraftforge/mcmaven/')
    }
}

compileJava {
    options.encoding = 'UTF-8'
}

changelog {
    from '0.0'
}

publishing {
    publications.register('mavenJava', MavenPublication).configure {
        artifactId = project.name
        from components.java

        pom {
            name = project.name
            description = 'Minecraft Maven Repo Generator'
            url = 'https://github.com/MinecraftForge/minecraft-maven'

            gradleutils.pom.setGitHubDetails(pom, 'minecraft-maven')

            license gradleutils.pom.licenses.LGPLv2_1

            developers {
                developer gradleutils.pom.Developers.LexManos
            }
        }
    }
    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

idea {
    module {
        // IntelliJ IDEA does not do this by itself anymore...
        downloadJavadoc = true
        downloadSources = true
    }
}
