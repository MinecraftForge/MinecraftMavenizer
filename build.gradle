import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    alias libs.plugins.licenser
    alias libs.plugins.gradleutils
    alias libs.plugins.shadow
    //alias libs.plugins.modules
}

final projectDisplayName = 'Minecraft Mavenizer'
final projectArtifactId = base.archivesName = 'minecraft-maven'
final projectVendor = 'Forge Development LLC'
description = 'A pure-blooded Java tool to generate a maven repository for Minecraft artifacts.'
group = 'net.minecraftforge'
version = gitversion.tagOffset

println "Version: $version"

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

dependencies {
    implementation libs.jopt
    implementation libs.gson
    implementation libs.jver
    implementation libs.srgutils
    implementation libs.diff

    implementation libs.fastcsv
    implementation libs.commonsio

    implementation libs.bundles.utils

    // Static Analysis
    compileOnly libs.nulls
}

/* Can't do modules yet with JavaProvisioner having JavaProbe in the root package.
extraJavaModuleInfo {
    failOnMissingModuleInfo = false
    //skipLocalJars = true

    automaticModule(libs.diff, 'io.codechicken.diffpatch')
}
*/

license {
    header = rootProject.file('LICENSE-header.txt')
    newLine false
    exclude '**/ComparableVersion.java' // Apache class used as source-level dep as to not need the hundred+ other classes the library pulls in
}

tasks.named('jar', Jar) {
    manifest {
        attributes([
            'Main-Class': 'net.minecraftforge.mcmaven.cli.Main'
        ])
        attributes([
            'Specification-Title'   : projectDisplayName,
            'Specification-Vendor'  : projectVendor,
            'Specification-Version' : gitversion.info.tag,
            'Implementation-Title'  : projectDisplayName,
            'Implementation-Vendor' : projectVendor,
            'Implementation-Version': project.version
        ], 'net/minecraftforge/mcmaven/cli/')
    }

    archiveClassifier = 'slim'
}

tasks.named('shadowJar', ShadowJar) {
    minimize {
        exclude dependency(libs.jver.get())
    }

    archiveClassifier = ''
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.addAll(['--module-version', project.version]);
}

changelog {
    fromBase()
    publishAll = false
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.shadow

        artifactId = projectArtifactId
        changelog.publish it

        pom { pom ->
            name = projectDisplayName
            description = project.description

            gradleutils.pom.setGitHubDetails pom

            licenses {
                license gradleutils.pom.licenses.LGPLv2_1
            }

            developers {
                developer gradleutils.pom.developers.LexManos
                developer gradleutils.pom.developers.Jonathing
            }
        }
    }

    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

idea.module { downloadSources = downloadJavadoc = true }
