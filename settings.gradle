plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.10.0'
}

rootProject.name = 'minecraft-maven'

dependencyResolutionManagement {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
        mavenLocal()
    }

    //@formatter:off
    versionCatalogs.register('libs') {
        plugin 'licenser',            'net.minecraftforge.licenser'        version '1.2.0'
        plugin 'gradleutils',         'net.minecraftforge.gradleutils'     version '2.5.1'
        plugin 'shadow',              'com.gradleup.shadow'                version '9.0.0-beta13'

        library 'gson',     'com.google.code.gson', 'gson'         version '2.10.1'
        library 'jopt',     'net.sf.jopt-simple',   'jopt-simple'  version '6.0-alpha-3'
        library 'jver',     'net.minecraftforge',   'java-version' version '1.0.4'
        library 'srgutils', 'net.minecraftforge',   'srgutils'     version '0.5.11'
        library 'diff',     'io.codechicken',       'DiffPatch'    version '2.0.0.36' // Fuzzy patching

        library 'fastcsv',   'de.siegmar', 'fastcsv'    version '3.4.0'
        library 'commonsio', 'commons-io', 'commons-io' version '2.18.0'

        // Utilities
        library 'utils-download', 'net.minecraftforge', 'download-utils'  version '0.3.0'
        library 'utils-files',    'net.minecraftforge', 'file-utils'      version '0.3.0'
        library 'utils-hash',     'net.minecraftforge', 'hash-utils'      version '0.1.9'
        library 'utils-data',     'net.minecraftforge', 'json-data-utils' version '0.2.1'
        library 'utils-logging',  'net.minecraftforge', 'log-utils'       version '0.3.0'
        bundle 'utils', ['utils-download', 'utils-files', 'utils-hash', 'utils-data', 'utils-logging']

        // Static Analysis
        library 'nulls', 'org.jetbrains', 'annotations' version '26.0.2'
    }
    //@formatter:on
}
