plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '1.0.0'
}

rootProject.name = 'minecraft-mavenizer'

// Applying plugins causes them to not have any IDE support when also applied to any build.gradle files
// The workaround for now is to use this listener here so that it can stay in settings.gradle
// See: https://youtrack.jetbrains.com/issue/IDEA-332061/Gradle-Missing-Code-Completion-Suggestions-for-Settings-Plugins-in-Groovy-DSL
gradle.beforeProject { Project project ->
    project.pluginManager.withPlugin('net.minecraftforge.gradleutils') {
        project.repositories {
            mavenCentral()
            maven project.gradleutils.forgeMaven
            //mavenLocal()
        }
    }
}

//formatter:off
dependencyResolutionManagement.versionCatalogs.register('libs') {
    plugin 'licenser',    'net.minecraftforge.licenser'    version '1.2.0'
    plugin 'gradleutils', 'net.minecraftforge.gradleutils' version '3.3.21'
    plugin 'gitversion',  'net.minecraftforge.gitversion'  version '3.1.6'
    plugin 'changelog',   'net.minecraftforge.changelog'   version '3.1.3'
    plugin 'shadow',      'com.gradleup.shadow'            version '9.2.2'

    // Static Analysis
    library 'nulls', 'org.jetbrains', 'annotations' version '26.0.2-1'

    library 'gson',     'com.google.code.gson', 'gson'             version '2.10.1'
    library 'jopt',     'net.sf.jopt-simple',   'jopt-simple'      version '6.0-alpha-3'
    library 'jver',     'net.minecraftforge',   'java-provisioner' version '2.0.2'
    library 'srgutils', 'net.minecraftforge',   'srgutils'         version '0.5.14'
    library 'diff',     'io.codechicken',       'DiffPatch'        version '2.0.0.36' // Fuzzy patching

    library 'fastcsv',   'de.siegmar', 'fastcsv'    version '3.4.0'
    library 'commonsio', 'commons-io', 'commons-io' version '2.18.0'

    // Utilities
    library 'utils-download', 'net.minecraftforge', 'download-utils'  version '0.4.0'
    library 'utils-files',    'net.minecraftforge', 'file-utils'      version '0.3.2'
    library 'utils-hash',     'net.minecraftforge', 'hash-utils'      version '0.1.12'
    library 'utils-data',     'net.minecraftforge', 'json-data-utils' version '0.4.1'
    library 'utils-logging',  'net.minecraftforge', 'log-utils'       version '0.5.0'
    library 'utils-os',       'net.minecraftforge', 'os-utils'        version '0.1.0'
    bundle 'utils', ['utils-download', 'utils-files', 'utils-hash', 'utils-data', 'utils-logging', 'utils-os']
}
//formatter:on
